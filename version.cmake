message(STATUS "Create version")

if(NOT NAMESPACE)
    message(FATAL_ERROR "Namespace not set")
endif()

message(STATUS "Namespace ${NAMESPACE} OUTPUT ${OUTPUT}")

execute_process(COMMAND git -C ${CMAKE_CURRENT_LIST_DIR} log --pretty=format:'%h' -n 1 OUTPUT_VARIABLE GIT_REV ERROR_QUIET)
execute_process(COMMAND git -C ${CMAKE_CURRENT_LIST_DIR} log --pretty=format:'%ai' -n 1 OUTPUT_VARIABLE GIT_TIME ERROR_QUIET)

# Check whether we got any revision (which isn't
# always the case, e.g. when someone downloaded a zip
# file from Github instead of a checkout)
if ("${GIT_REV}" STREQUAL "")
    set(GIT_REV "N/A")
    set(GIT_DIFF "")
    set(GIT_TAG "N/A")
    set(GIT_BRANCH "N/A")
    set(GIT_DATE "N/A")
else()
    execute_process(COMMAND bash -c "git -C ${CMAKE_CURRENT_LIST_DIR} diff --quiet --exit-code || echo .x" OUTPUT_VARIABLE GIT_DIFF)
    execute_process(COMMAND git -C ${CMAKE_CURRENT_LIST_DIR} describe --exact-match --tags OUTPUT_VARIABLE GIT_TAG ERROR_QUIET)
    execute_process(COMMAND git -C ${CMAKE_CURRENT_LIST_DIR} rev-parse --abbrev-ref HEAD OUTPUT_VARIABLE GIT_BRANCH)

    string(STRIP "${GIT_REV}" GIT_REV)
    string(SUBSTRING "${GIT_REV}" 1 7 GIT_REV)
    string(STRIP "${GIT_DIFF}" GIT_DIFF)
    string(STRIP "${GIT_TAG}" GIT_TAG)
    string(STRIP "${GIT_BRANCH}" GIT_BRANCH)
    string(SUBSTRING "${GIT_TIME}" 1 10 GIT_DATE)
endif()


set(VERSION "#include \"${NAMESPACE}_version.h\"
const std::string pml::${NAMESPACE}::GIT_REV=\"${GIT_REV}${GIT_DIFF}\";
const std::string pml::${NAMESPACE}::GIT_TAG=\"${GIT_TAG}\";
const std::string pml::${NAMESPACE}::GIT_BRANCH=\"${GIT_BRANCH}\";
const std::string pml::${NAMESPACE}::GIT_DATE=\"${GIT_DATE}\";
unsigned int pml::${NAMESPACE}::VERSION_MAJOR=${MAJOR};
unsigned int pml::${NAMESPACE}::VERSION_MINOR=${MINOR};
unsigned int pml::${NAMESPACE}::VERSION_PATCH=${PATCH};
const std::string pml::${NAMESPACE}::VERSION_STRING=\"${MAJOR}.${MINOR}.${PATCH}-${GIT_REV}${GIT_DIFF}\";
const std::string pml::${NAMESPACE}::CMAKE_DATE = \"${BUILD_DATE}\";
const std::string pml::${NAMESPACE}::BUILD_TIME = std::string(__DATE__)+\" \"+std::string(__TIME__);
")

set(SRC_FILE ${OUTPUT}/src/${NAMESPACE}_version.cpp)
if(EXISTS ${SRC_FILE})
    file(READ ${SRC_FILE} VERSION_)
else()
    set(VERSION_ "_")
endif()

if (NOT "${VERSION}" STREQUAL "${VERSION_}")
    message(STATUS "Write to ${SRC_FILE}")
    file(WRITE ${SRC_FILE} "${VERSION}")
else()
    message(STATUS "No changes to ${SRC_FILE}")
endif()

#now do the include file
set(INCLUDE "#ifndef __${NAMESPACE}_VERSION_H__
#define __${NAMESPACE}_VERSION_H__
#include <string>
// These variables are autogenerated and compiled
// into the library by the version.cmake script
namespace pml
{
    namespace ${NAMESPACE}
    {
        extern const std::string GIT_TAG;
        extern const std::string GIT_REV;
        extern const std::string GIT_BRANCH;
        extern const std::string GIT_DATE;
        extern unsigned int VERSION_MAJOR;
        extern unsigned int VERSION_MINOR;
        extern unsigned int VERSION_PATCH;
        extern const std::string VERSION_STRING;
        extern const std::string CMAKE_DATE;
        extern const std::string BUILD_TIME;
    }   
}
#endif")

set(INC_FILE ${OUTPUT}/include/${NAMESPACE}_version.h)
if(EXISTS ${INC_FILE})
    file(READ ${INC_FILE} INCLUDE_)
else()
    set(INCLUDE_ "")
endif()

if (NOT "${INCLUDE}" STREQUAL "${INCLUDE_}")
    message(STATUS "Write to ${INC_FILE}")
    file(WRITE ${INC_FILE} "${INCLUDE}")
endif()